( hello-animated-sprite.tal )

( devices )
|00 @System  [ &vector $2 &pad $6  &r $2 &g $2 &b $2 ]
|10 @Console [ &vector $2 &read $1 &pad $5 &write $1 &error $1 ]
|20 @Screen  [ &vector $2 &width $2 &height $2 &pad $2 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1 ]
|80 @Controller [ &vector $2 &button $1 &key $1 ]

( macros )
%EMIT { .Console/write DEO }
%HALF { #01 SFT2 } ( short -- short/2 )
%color-clear { #10 }
%color-2-3 { #4e }
%MOD { DUP2 DIV MUL SUB } ( a b -- a%b )
%MOD2 { OVR2 OVR2 DIV2 MUL2 SUB2 } ( a b -- a%b )
%MOD8 { #07 ADD } ( byte -- byte%8 )
%TO-SHORT { #00 SWP } ( byte -- short )
%check-width { .Screen/width DEI2 MOD2 
               .sprite/pos-x STZ2 
               ,&draw JMP }
%check-height { .Screen/height DEI2 MOD2
                .sprite/pos-y STZ2
                ,&draw JMP }

( zero page )
|0000
@sprite [ &pos-x $2 &pos-y $2 ]
@framecount $1 

( init )
|0100
    ( set system colors )
    #2ce9 .System/r DEO2
    #01c0 .System/g DEO2
    #2ce5 .System/b DEO2

    ( set sprite address )
    ;animation .Screen/addr DEO2
    ( set initial sprite position )
    .Screen/width DEI2 HALF #0004 SUB2 .sprite/pos-x STZ2
    .Screen/height DEI2 HALF #0004 SUB2 .sprite/pos-y STZ2
    ( set screen vector )
    ;on-frame .Screen/vector DEO2
BRK

@on-frame ( -> )
    ( 0: increment framecount )
    .framecount LDZ INC .framecount STZ

    ( 1: clear sprite )
    color-clear .Screen/sprite DEO ( draw in fg w bg color0 )
 
    ( 2: change position )
    &check-arrows
        .Controller/button DEI
        #10 AND ( isolate bit 4, UP )
        ,&up JCN
        
        .Controller/button DEI
        #20 AND ( isolate bit 5, DOWN )
        ,&down JCN

        .Controller/button DEI
        #40 AND ( isolate bit 6, LEFT )
        ,&left JCN ( jump if not 0 )

        .Controller/button DEI
        #80 AND ( isolate bit 7, RIGHT )
        ,&right JCN

    ,&draw JMP

    &left
        .sprite/pos-x LDZ2 #0001 SUB2 
        check-width
    &right
        .sprite/pos-x LDZ2 INC2
        check-width
    &up
        .sprite/pos-y LDZ2 #0001 SUB2 
        check-height
    &down
        .sprite/pos-y LDZ2 INC2 
        check-height
    ( 3: draw sprite )
    &draw
        ( calculate animation frame w/ pointer arithmetic )
        .framecount LDZ MOD8 #08 MUL TO-SHORT ;animation ADD2
        .Screen/addr DEO2 

        ( load x coord from zpage and send to screen )
        .sprite/pos-x LDZ2 .Screen/x DEO2
        .sprite/pos-y LDZ2 .Screen/y DEO2

        ( draw sprite in fg w color 2 and transparency )
        color-2-3 .Screen/sprite DEO

BRK

( sprites )
@animation [
  &frame0 00 00 00 00 00 00 01 03
  &frame1 00 00 00 00 01 03 06 0c
  &frame2 00 00 01 03 06 0c 18 30
  &frame3 01 03 06 0c 18 30 60 c0
  &frame4 03 06 0c 18 30 60 c0 80
  &frame5 0c 18 30 60 c0 80 00 00
  &frame6 30 60 c0 80 00 00 00 00
  &frame7 c0 80 00 00 00 00 00 00 ]
